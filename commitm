#!/usr/bin/env perl

use v5.10;
use utf8;
use strict;
use FindBin qw($Bin $Script);
use YAML::XS qw(LoadFile);
use Getopt::Long;
use Try::Catch;
use Function::Parameters;

fun load_messages($lang = undef) {
    my $messages;
    my $msg_file = "$Bin/$Script.d/" . ($lang ? "lang/$lang" : 'messages') .
                   '.yml';

    try {
        $messages = LoadFile($msg_file);
    }
    catch {
        die "Failed loading $msg_file: $_";
    };

    unless (ref $messages eq 'ARRAY') {
        die "No message list for ‘$lang’ found";
    }

    return $messages;
}

fun main() {
    my $lang;
    try {
        GetOptions('language=s', \$lang);
    }
    catch {
        die 'Failed getting command line arguments.';
    };

    my $messages;
    try {
        $messages = load_messages($lang);
    }
    catch {
        die "Messages not retrieved: $_";
    };

    say $messages->[rand @$messages];
}

binmode(STDOUT, ':encoding(UTF-8)');
main();
