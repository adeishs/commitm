#!/usr/bin/env perl

use v5.10;
use utf8;
use strict;
use FindBin qw($Bin $Script);
use YAML::XS qw(LoadFile);
use Getopt::Long;
use Try::Catch;
use Function::Parameters;
use Pod::Usage qw(pod2usage);

fun load_messages($lang = undef) {
    my $messages;
    my $msg_file = "$Bin/$Script.d/" . ($lang ? "lang/$lang" : 'messages') .
                   '.yml';

    try {
        $messages = LoadFile($msg_file);
    }
    catch {
        die "Failed loading $msg_file: $_";
    };

    unless (ref $messages eq 'ARRAY') {
        die "No message list for ‘$lang’ found";
    }

    return $messages;
}

fun main() {
    my $lang;
    my $help = 0;
    try {
        GetOptions(
            'help' => \$help,
            'language=s' => \$lang,
        );
    }
    catch {
        die 'Failed getting command line arguments.';
    };

    if ($help) { pod2usage(-verbose => 2); }

    my $messages;
    try {
        $messages = load_messages($lang);
    }
    catch {
        die "Messages not retrieved: $_";
    };

    say $messages->[rand @$messages];
}

binmode(STDOUT, ':encoding(UTF-8)');
main();

__END__

=encoding utf-8

=head1 NAME

commitm — Commit Messages Done Quickly!

=head1 SYNOPSIS

commitm [options]

=head1 OPTIONS

Options:

 -help       Display help
 -lang       Use a language-specific message
 --language  Same as -lang

If no language is specified, a language-agnostic message will be output.

=head1 DESCRIPTION

Who has much time writing a commit message? Just generate it!

=cut
